// Main project (located in apps/)
buildscript {
    repositories {
        mavenCentral()
    }

    ext {
        set('springCloudVersion', "Hoxton.SR12")
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.7.12'
    }
}

plugins {
    id 'application'
}

sourceSets {
    main {
        java { srcDirs = ['apps/main'] }
        resources { srcDirs = ['apps/main/resources'] }
    }

    test {
        java { srcDirs = ['apps/test'] }
        resources { srcDirs = ['apps/test/resources'] }
    }
}

application {
    mainClass = 'carlos.projects.apps.Starter'
}

// Common for all projects
allprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    dependencies {
        // Prod
        implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
        implementation 'com.vlkan.log4j2:log4j2-logstash-layout:0.21'
        implementation 'mysql:mysql-connector-java:8.0.18'

        // Test
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
        testImplementation 'org.mockito:mockito-core:2.+'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }


    task view_paths {
        doLast { task ->
            println "$task.project.name"
            println "------------------"
            println "Main: $sourceSets.main.java.srcDirTrees"
            println "   Resources: $sourceSets.main.resources.srcDirTrees"
            println "Tests: $sourceSets.test.java.srcDirTrees"
            println "   Resources: $sourceSets.test.resources.srcDirTrees"
        }
    }
}

// All subprojects (located in src/*)
subprojects {
    group = "carlos.projects.${rootProject.name}"

    sourceSets {
        main {
            java { srcDirs = ['main'] }
            resources { srcDirs = ['main/resources'] }
        }

        test {
            java { srcDirs = ['test'] }
            resources { srcDirs = ['test/resources'] }
        }
    }

    dependencies {
        // Implementations source -> https://central.sonatype.com/
        implementation 'org.springframework.boot:spring-boot-starter-web:2.1.8.RELEASE'
        implementation 'org.hibernate:hibernate-core:6.2.2.Final'
        implementation 'org.springframework:spring-orm:5.3.27'
        implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
        implementation 'org.reflections:reflections:0.9.5-RC2'
        // We will use it for the pull of connections in production (hibernate gives us one for default)
        implementation 'org.apache.tomcat:tomcat-dbcp:10.1.2'

        // Implementations to read in a good way the xml (to have and read errors, etc).
        implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
        implementation 'com.sun.xml.bind:jaxb-impl:4.0.2'

        // All our subprojects are gonna deppend on the rootProject.
        testImplementation rootProject.sourceSets.main.output


        //Tests implementations
        testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.8.RELEASE'
        testImplementation 'net.datafaker:datafaker:1.9.0'

        if (project.name != "shared") {
            implementation project(":shared")
            testImplementation project(":shared").sourceSets.test.output
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}

bootJar {
    archiveBaseName = 'students-courses-ddd'
    archiveVersion = '0.0.1'
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.1.8.RELEASE'

    implementation project(":shared")
    implementation project(":backoffice")
    implementation project(":mooc")

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.8.RELEASE'
}



